//===== rAthena Script =======================================
//= Euphy's WOE Controller
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.5
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= A controller for War of Emperium designed for
//= simplicity and ease of use.
//= Many concepts taken from ToastOfDoom's script,
//= and "rewards" function originally by Goddameit.
//===== Additional Comments: =================================
//= Be sure to disable the default agit controllers! ตรวจดูให้แน่ใจว่าปิด agit controllers!
//== npc\guild\agit_controller.txt
//== npc\guild2\agit_start_se.txt
//============================================================

// Information NPC
//============================================================
morocc,170,113,4	script	WOE Information	835,{
	doevent "WOE_CONTROL::OnMenu";
	end;
OnAgitStart:
	while(agitcheck()) {
		specialeffect EF_BEGINSPELL6;
		sleep 425;
	}
	end;
}

// Script Core
//============================================================
-	script	WOE_CONTROL	-1,{
function Disp_Owner; function Add_Zero;

OnInit:

// -----------------------------------------------------------
//  Configuration settings. ตั้งค่า
// -----------------------------------------------------------

	set .CastleWarp,0;		// 1: Always enable all castle warps. | 0: Warp only to active castles. 1.เปิดทุกวาร์ปปราสาท 0.เปิดวาร์ปเฉพาะปราสาทที่วอร์
	set .AutoKick,1;		// Automatically kick players from inactive castles during WOE? (1:yes / 0:no) เตะผู้เล่นทุกคนที่อยู่ในปราสาทก่อนวอร์ (1.ใช่/0.ไม่)
	set .NoOwner,0; 		// Automatically kick players from unconquered castles outside of WOE? (1:yes / 0:no) เตะผู้เล่นทุกคนที่ไม่ใช่เจ้าของปราสาทก่อนวอร์ (1.ใช่/0.ไม่)
	set .ExitWarp,0;		// Warp all players from castles when WOE ends? (1:yes / 0:no) วาร์ปผู้เล่นทุกคนออกจากปราสาทหลังจบวอร์ (1.ใช่/0.ไม่)
	set .GMAccess,99;		// GM level required to access Session Manager. เลเวลกรุ๊ปจีเอ็มในการจัดการคำสั่ง

// -----------------------------------------------------------
//  Reward options. ระบบรางวัล
// -----------------------------------------------------------
//  [1] Enable rewards. เปิดให้มีรางวัล
//  [2] Mail all rewards. ส่งรางวัลทางเมลล์
//      - If not set, players receive items in their inventory. ถ้าไม่ได้ตั้งไว้ ผู้เล่นจะได้รับทางช่องเก็บของแทน
//      - Only ONE item can be sent via mail, plus Zeny. ส่งของทางเมลล์ได้ชิ้นเดียว บวกเงิน
//      - Note that offline players do NOT receive rewards. ผู้เล่นที่ไม่ออนอด
//  [4] Only reward Guild Masters. เฉพาะหัวกิลด์ที่ได้รับรางวัล
//      - If not set, all guild members are rewarded. ถ้าไม่ได้ตั้งไว้ สมาชิกทุกคนจะได้รับของรางวัล
//      - If mailing is enabled (option 2), offline Guild Masters WILL receive rewards. ถ้าเปิดการส่งทางเมลล์ (option 2) หัวกิลด์ไม่ออนก็จะได้รับทางเมลล์
//  [8] Duplicate IP check. ตรวจไอพีซ้ำ
//      - Members in a guild with the same IP address are not rewarded. สมาชิกกิลด์ที่มีไอพีซ้ำกันจะไม่ได้ของรางวัล
//      - If Guild Masters is enabled (option 4), this feature is not used. ถ้าเปิดระบบหัวกิลด์ (option 4) ฟังก์ชั่นนี้จะใช้ไม่ได้
// -----------------------------------------------------------

	// Combine values as needed (e.g. 1|8 = 1+8 = 9). วิธีรวมสิ่งที่ต้องการ (ตัวอย่าง 1|8 = 1+8 = 9).
	set .Options, 1|8;

	// Rewards per castle. ของรางวัลต่อปราสาท
	// -- when given directly: <itemID>,<amount>{,<itemID>,<amount>,...} ของที่จะให้ : <ไอเท็มไอดี>,<จำนวน>{,<ไอเท็มไอดี>,<จำนวน>,...} 
	// -- via mail (option 2): <itemID>,<amount>,<Zeny> ถ้าส่งทางเมลล์ (option 2): <ไอเท็มไอดี>,<จำนวน>,<เงิน>
	setarray .Reward[0],14001,1;

// -----------------------------------------------------------
//  Constants (leave this section alone).
// -----------------------------------------------------------

	setarray .Castles$[0],	// ตั้งค่าชั่วคราวของปราสาท
		"prtg_cas01","prtg_cas02","prtg_cas03","prtg_cas04","prtg_cas05",
		"payg_cas01","payg_cas02","payg_cas03","payg_cas04","payg_cas05",
		"gefg_cas01","gefg_cas02","gefg_cas03","gefg_cas04","gefg_cas05",
		"aldeg_cas01","aldeg_cas02","aldeg_cas03","aldeg_cas04","aldeg_cas05",
		"arug_cas01","arug_cas02","arug_cas03","arug_cas04","arug_cas05",
		"schg_cas01","schg_cas02","schg_cas03","schg_cas04","schg_cas05";
	setarray .EndLabel$[0],"ar01","ar02","ar03","ar04","ar05","sc01","sc02","sc03","sc04","sc05";	// ไม่รู้มันแปลว่า ป้ายจบ? ไม่ต้องสนใจมัน
	setarray .Days$[0],"วันอาทิตย์","วันจันทร์","วันอังคาร","วันพุธ","วันพฤหัสบดี","วันศุกร์","วันเสาร์";	// วัน
	setarray .Regions$[0],"Prontera","Payon","Geffen","Aldebaran","Arunafeltz","Schwaltzvalt";	// เมือง
	setarray .Map$[0],"prt_gld","pay_gld","gef_fild13","alde_gld","aru_gld","sch_gld";	// แผนที่
	setarray .MapX[0],134,240,153,111,208,121,295,317,140,204,214,308,143,193,305,48, 95,142,239,264,158,83, 68,299,292,293,288, 97,137, 71; // จุด X
	setarray .MapY[0], 65,128,137,240,240,233,116,293,160,266, 75,240,240,278, 87,83,249, 85,242, 90,272,47,155,345,107,100,252,196, 90,315;	// จุด Y

// -----------------------------------------------------------

	set .Size, getarraysize($WOE_CONTROL);
	if (.AutoKick || .NoOwner)
		for(set .@i,0; .@i<30; set .@i,.@i+1) {
			setmapflag .Castles$[.@i], mf_loadevent;
			setd "."+.Castles$[.@i], .@i;
		}
	if (!agitcheck() && !agitcheck2()) sleep 4000;
	set .Init,1;

OnMinute00:
	freeloop(1);
	if (agitcheck() || agitcheck2()) {
		for(set .@i,0; .@i<.Size; set .@i,.@i+4)
			if (gettime(DT_DAYOFWEEK) == $WOE_CONTROL[.@i] && gettime(DT_HOUR) == $WOE_CONTROL[.@i+2]) {
			OnWOEEnd:
				announce "สงครามตีปราสาทได้จบลงแล้ว!",bc_all|bc_woe;
				AgitEnd; AgitEnd2;
				sleep 1000;
				for(set .@j,0; .@j<30; set .@j,.@j+1) {
					if (.Active[0]&(1<<.@j)) Disp_Owner(.Castles$[.@j],1);
					if (.ExitWarp) maprespawnguildid .Castles$[.@j],0,3;
				}
				if ((.Options&1) && .Active[0] && .ForceEnd != 2) callsub OnReward, .Active[0];
				deletearray .Active[0],2;
				if (.ForceEnd) { set .ForceEnd,0; end; }
				break;
			}
	}
	if ((!agitcheck() && !agitcheck2()) || .Init) {
		if (!agitcheck() && !agitcheck2()) set .Init,0;
		for(set .@i,0; .@i<.Size; set .@i,.@i+4)
			if (gettime(DT_DAYOFWEEK) == $WOE_CONTROL[.@i] && gettime(DT_HOUR) >= $WOE_CONTROL[.@i+1] && gettime(DT_HOUR) < $WOE_CONTROL[.@i+2]) {
				deletearray .Active[0],2;
				set .Active[0], $WOE_CONTROL[.@i+3];
				if (.Init) { AgitEnd; AgitEnd2; }
				else announce "สงครามตีปราสาทได้เริ่มขึ้นแล้ว!",bc_all|bc_woe;
				sleep 1000;
				AgitStart; AgitStart2;
				for(set .@j,0; .@j<30; set .@j,.@j+1) {
					if (.Active[0]&(1<<.@j)) {
						if (!.Init) Disp_Owner(.Castles$[.@j],0);
						set .Active[1], .Active[1] | (1<<((.@j/5)+1));
					} else {
						if (.@j<20) {
							donpcevent "Agit#"+.Castles$[.@j]+"::OnAgitEnd";
							killmonster .Castles$[.@j], "Agit#"+.Castles$[.@j]+"::OnAgitBreak";
						} else {
							donpcevent "Manager#"+.Castles$[.@j]+"::OnAgitEnd2";
							killmonster .Castles$[.@j], "Steward#"+.EndLabel$[.@j-20]+"::OnStartArena";
						}
					}
				}
				break;
			}
	}
	set .Init,0;
	end;

function Disp_Owner {
	set .@o, getcastledata(getarg(0),1);
	if (.@o) announce "ปราสาท ["+getcastlename(getarg(0))+"] ในตอนนี้ "+((getarg(1))?"ได้ถูกยึดครอง":"ครอบครอง")+" โดย ["+getguildname(.@o)+"] guild.",bc_all|bc_woe;
	else announce "ปราสาท ["+getcastlename(getarg(0))+"] ในตอนนี้ยังไม่มีเจ้าของ.",bc_all|bc_woe;
	return;
}
function Add_Zero {
	return ((getarg(0)<10)?"0":"")+getarg(0)+(getarg(1,0)?".":":")+"00";
}

OnReward:
	set .@sql$, ((.Options&4)?"position = 0":"online = 1");
	if (.Options&2) set .@str$,gettimestr("%B %d, %Y",21);
	freeloop(1);
	for(set .@i,0; .@i<30; set .@i,.@i+1)
		if (getarg(0)&(1<<.@i)) {
			set .@gid, getcastledata(.Castles$[.@i],1);
			if (!.@gid) continue;
			set .@size, query_sql("SELECT account_id,char_id FROM `guild_member` WHERE guild_id = '"+.@gid+"' AND "+.@sql$,.@aid,.@cid);
			for(set .@j,0; .@j<.@size; set .@j,.@j+1) {
				if ((.Options&8) && !(.Options&4)) {
					set .@ip$, replacestr(getcharip(.@aid[.@j]),".","a");
					if (getd(".@ip_"+.@i+"_"+.@ip$)) continue;
					setd ".@ip_"+.@i+"_"+.@ip$,1;
				}
				if (.Options&2) {
					query_sql("INSERT INTO `mail` (send_name,dest_id,title,message,nameid,amount,identify,zeny,time) VALUES ("+
					          "'no-reply',"+.@cid[.@j]+",'** Siege Reward: "+getcastlename(.Castles$[.@i])+" **',"+
					          "'Brave one,% % Congratulations!% Your guild has successfully occupied% territory in the War of Emperium on% "+.@str$+".% % % % % [ Your reward is attached. ]',"+
					          .Reward[0]+","+.Reward[1]+",0,"+.Reward[2]+",UNIX_TIMESTAMP(NOW()))");
					if (!getd(".@str_"+.@cid[.@j]) && isloggedin(.@aid[.@j],.@cid[.@j])) {
						setd ".@str_"+.@cid[.@j],1;
						message rid2name(.@aid[.@j]),"เจ้าได้รับจดหมาย! กรุณาออกเข้าใหม่เพื่ออัพเดตจดหมาย.";
					}
				} else if (isloggedin(.@aid[.@j])) {
					for(set .@k,0; .@k<getarraysize(.Reward); set .@k,.@k+2)
						getitem .Reward[.@k], .Reward[.@k+1], .@aid[.@j];
					message rid2name(.@aid[.@j]),"เจ้าได้รับรางวัลสำหรับการยึดปราสาท "+getcastlename(.Castles$[.@i])+".";
				}
			}
		}
	if (.Options&2) query_sql("UPDATE `mail` SET message = REPLACE(message,'%',CHAR(13)) WHERE send_name = 'no-reply'");
	return;

OnPCLoadMapEvent:
	if (!compare(strcharinfo(3),"g_cas")) end;
	if (((.AutoKick && .Active[0]) || (.NoOwner && !getcastledata(strcharinfo(3),1))) && !(.Active[0]&(1<<getd("."+strcharinfo(3))))) {
		if (getcharid(2) && getcastledata(strcharinfo(3),1) == getcharid(2)) end;
		sleep2 1000;
		message strcharinfo(0), getcastlename(strcharinfo(3))+" ในตอนนี้ไม่ได้เปิด.";
		sleep2 5000;
		if (compare(strcharinfo(3),"g_cas")) warp "SavePoint",0,0;
	}
	end;

OnMenu:
while(1) {
	mes "[รายละเอียด WOE]";
	if (agitcheck() || agitcheck2()) {
		if (.Active[0]) {
			for(set .@i,0; .@i<.Size; set .@i,.@i+4)
				if (gettime(DT_DAYOFWEEK) == $WOE_CONTROL[.@i] && gettime(DT_HOUR) >= $WOE_CONTROL[.@i+1] && gettime(DT_HOUR) < $WOE_CONTROL[.@i+2]) {
					set .@i, $WOE_CONTROL[.@i+2];
					break;
				}
			mes "สงครามตีปราสาท ^0055FFเริ่ม^000000 ตั้งแต่ ^FF0000"+Add_Zero(.@i)+"^000000 ในเมืองต่อไปนี้:";
			mes " ";
			for(set .@i,0; .@i<6; set .@i,.@i+1)
				if (.Active[1]&(1<<(.@i+1))) mes " > ^777777"+.Regions$[.@i]+"^000000";
		} else
			mes "สงครามตีปราสาท ^0055FFเริ่มแล้ว^000000.";
	} else {
		for(set .@i,0; .@i<.Size; set .@i,.@i+4)
			if ((gettime(DT_DAYOFWEEK) == $WOE_CONTROL[.@i] && gettime(DT_HOUR) <= $WOE_CONTROL[.@i+1]) || gettime(DT_DAYOFWEEK) < $WOE_CONTROL[.@i]) {
				setarray .@time[0],$WOE_CONTROL[.@i],$WOE_CONTROL[.@i+1];
				break;
			}
		if (!getarraysize(.@time))
			setarray .@time[0],$WOE_CONTROL[0],$WOE_CONTROL[1];
		mes "สงครามตีปราสาท ^777777จบแล้ว^000000.";
		if (.Size) {
			mes " ";
			mes "สงครามครั้งถัดไปจะเริ่ม";
			mes "ใน ^0055FF"+.Days$[.@time[0]]+"^000000 ที่ "+Add_Zero(.@time[1])+"^000000.";
		}
	}
	next;
	switch(select(""+((.CastleWarp || .Active[1])?" ~ วาร์ปไปปราสาท...":"")+": ~ ตรวจดูตารางเวลา...: ~ ดูเจ้าของปราสาท...:"+((getgmlevel()<.GMAccess || !getgmlevel())?"":" ~ จัดการรอบถัดไป...")+": ~ ^777777ยกเลิก^000000")) {
	case 1:
		if (.CastleWarp) set .@clr$,"^0055FF";
		set .@menu$,"";
		for(set .@i,0; .@i<6; set .@i,.@i+1) {
			if (.CastleWarp || .Active[1]&(1<<(.@i+1)))
				set .@menu$, .@menu$+" ~ "+((.Active[1]&(1<<(.@i+1)))?.@clr$:"^777777")+.Regions$[.@i]+" Castles^000000";
			set .@menu$, .@menu$+":";
		}
		set .@i, select(.@menu$)-1;
		set .@menu$,"";
		for(set .@j,.@i*5; .@j<(.@i*5)+5; set .@j,.@j+1) {
			if (.CastleWarp || .Active[0]&(1<<.@j))
				set .@menu$, .@menu$+" ~ "+((.Active[0]&(1<<.@j))?.@clr$:"^777777")+getcastlename(.Castles$[.@j])+"^000000";
			set .@menu$, .@menu$+":";
		}
		set .@j, select(.@menu$)-1;
		warp .Map$[.@i],.MapX[(.@i*5)+.@j],.MapY[(.@i*5)+.@j];
		close;
	case 2:
		mes "[Schedule]";
		if (.Size) {
			freeloop(1);
			for(set .@i,0; .@i<.Size; set .@i,.@i+4) {
				mes "> ^FF0000"+.Days$[$WOE_CONTROL[.@i]]+" ("+Add_Zero($WOE_CONTROL[.@i+1])+"-"+Add_Zero($WOE_CONTROL[.@i+2])+")^000000";
				for(set .@j,0; .@j<30; set .@j,.@j+1)
					if ($WOE_CONTROL[.@i+3]&(1<<.@j)) mes "  ~ "+getcastlename(.Castles$[.@j])+" ^777777("+.Castles$[.@j]+")^000000";
				if (.@i+4 < .Size) mes " ";
			}
			freeloop(0);
		} else
			mes "ยังไม่ได้ตั้งเวลา.";
		next;
		break;
	case 3:
		mes "[Castle Ownership]";
		for(set .@i,0; .@i<6; set .@i,.@i+1) {
			set .@k, .@i*5;
			mes "> ^FF0000"+.Regions$[.@i]+"^000000";
			for(set .@j,.@k; .@j<(.@k+5); set .@j,.@j+1) { 
				set .@t, getcastledata(.Castles$[.@j],1);
				mes "  ~ "+getcastlename(.Castles$[.@j])+": "+((.@t)?"^0055FF"+getguildname(.@t):"^777777ยังไม่มีเจ้าของ")+"^000000";
			}
			if (.@i < 5) mes " ";
		}
		next;
		break;
	case 4:
	while(1) {
		mes "[Session Manager]";
		mes "มีอยู่ ^0055FF"+(.Size/4)+" รอบ(s)^000000 ตั้งค่า.";
		mes "เจ้าจะทำอะไร ?";
		next;
		switch(select(" ~ เพิ่มรอบ...: ~ ลบรอบ...: ~ Reload ตั้งค่า...:"+((agitcheck() || agitcheck2())?" ~ จบรอบวอร์...":"")+": ~ ^777777กลับไป^000000")) {
		case 1:
			mes "[New Session]";
			if (.Size > 127) {
				mes "เจ้าได้มีจำนวนสูงสุด 32 รอบ แล้ว.";
				next;
				break;
			}
			mes "เลือกวัน.";
			next;
			set .@Day, select(" ~ "+implode(.Days$,": ~ "))-1;
			mes "[New Session]";
			mes "เลือกเวลาเริ่ม ^0055FF"+.Days$[.@Day]+"^000000.";
			next;
			set .@menu$,"";
			for(set .@i,0; .@i<23; set .@i,.@i+1)
				set .@menu$, .@menu$+" ~ "+Add_Zero(.@i,1)+":";
			set .@Start, select(.@menu$)-1;
			mes "[New Session]";
			mes "เลือกเวลาจบ ^0055FF"+.Days$[.@Day]+"^000000.";
			next;
			set .@menu$,"";
			for(set .@i,.@Start+1; .@i<24; set.@i,.@i+1)
				set .@menu$, .@menu$+" ~ "+Add_Zero(.@i,1)+":";
			set .@End, select(.@menu$)+.@Start;
			for(set .@i,0; .@i<.Size; set .@i,.@i+4)
				if (.@Day == $WOE_CONTROL[.@i] &&
				   ((.@Start >= $WOE_CONTROL[.@i+1] && .@Start < $WOE_CONTROL[.@i+2]) ||
				    (.@End > $WOE_CONTROL[.@i+1] && .@End <= $WOE_CONTROL[.@i+2]) ||
				    (.@Start <= $WOE_CONTROL[.@i+1] && .@End >= $WOE_CONTROL[.@i+2]))) {
					mes "[New Session]";
					mes "เวลาที่เลือกซ้ำกับเวลาเก่าที่มีอยู่.";
					next;
					set .@d,1;
					break;
				}
			if (.@d) { set .@d,0; break; }
			set .@Castle,0;
			while(1) {
				mes "[New Session]";
				mes "^0055FF"+.Days$[.@Day]+" ("+Add_Zero(.@Start)+"-"+Add_Zero(.@End)+")^000000";
				mes " > Castles:";
				if (!.@Castle)
					mes "   ~ ^777777(ไม่ได้เลือก)^000000";
				else for(set .@i,0; .@i<30; set .@i,.@i+1)
					if (.@Castle&(1<<.@i)) mes "   ~ "+getcastlename(.Castles$[.@i])+" ("+.Castles$[.@i]+")";
				next;
				set .@menu$,((.@Castle)?" ~ ^FF0000Finished...^000000":"")+":";
				for(set .@i,0; .@i<30; set .@i,.@i+1)
					set .@menu$, .@menu$+" ~ "+((.@Castle&(1<<.@i))?"^0055FF":"")+getcastlename(.Castles$[.@i])+" ("+.Castles$[.@i]+")^000000:";
				set .@i, select(.@menu$)-1;
				if (.@i)
					set .@Castle, .@Castle^(1<<(.@i-1));
				else {
					mes "[New Session]";
					mes "เจ้าแน่ใจ ?";
					next;
					switch(select(" ~ ^0055FFเพิ่มรอบ...^000000: ~ เลือกปราสาทต่อไป...: ~ ^777777ยกเลิก^000000")) {
					case 1:
						for(set .@i,0; .@i<.Size; set .@i,.@i+4)
							if ((.@Day == $WOE_CONTROL[.@i] && .@End <= $WOE_CONTROL[.@i+1]) || .@Day < $WOE_CONTROL[.@i]) {
								set .@d,1;
								break;
							}
						if (!.@d) { set .@d,1; set .@i,.Size; }
						copyarray $WOE_CONTROL[.@i+4], $WOE_CONTROL[.@i], .Size-.@i;
						setarray $WOE_CONTROL[.@i], .@Day, .@Start, .@End, .@Castle;
						set .Size, getarraysize($WOE_CONTROL);
					case 3:
						mes "[New Session]";
						mes ((.@d)?"Session added.":"cancelled.");
						next;
						set .@d,1;
					case 2:
						break;
					}
					if (.@d) { set .@d,0; break; }
				}
			}
			break;
		case 2:
			mes "[Remove Session]";
			if (!.Size) {
				mes "ไม่มีรอบให้ ตั้งค่า.";
				next;
				break;
			}
			mes "เลือกรอบที่จะลบ.";
			next;
			set .@menu$,"";
			for(set .@i,0; .@i<.Size; set .@i,.@i+4)
				set .@menu$, .@menu$+" ~ "+.Days$[$WOE_CONTROL[.@i]]+" ("+Add_Zero($WOE_CONTROL[.@i+1],1)+"-"+Add_Zero($WOE_CONTROL[.@i+2],1)+"):";
			set .@menu$, .@menu$+" ~ ^777777ยกเลิก^000000";
			set .@i, select(.@menu$)-1;
			if (.@i == (.Size/4)) break;
			mes "[Remove Session]";
			mes "ลบ ^0055FF"+.Days$[$WOE_CONTROL[.@i*4]]+"'s^000000 รอบ ?";
			mes "ไม่สามารถยกเลิกการดำเนินการนี้ได้ระหว่างการลบ.";
			next;
			set .@j, select(" ~ ^FF0000ลบรอบ...^000000: ~ ^777777ยกเลิก^000000");
			mes "[Remove Session]";
			if (.@j == 2)
				mes "ยกเลิก.";
			else {
				deletearray $WOE_CONTROL[.@i*4],4;
				set .Size, getarraysize($WOE_CONTROL);
				mes "ลบแล้ว.";
			}
			next;
			break;
		case 3:
			mes "[Reload Settings]";
			mes "การดำเนินการนี้จะเรียกใช้กิจกรรมทั้งหมดที่เกี่ยวข้องกับการกำหนดรอบใหม่.";
			if (agitcheck() || agitcheck2()) {
				mes " ";
				mes "ระวังมันจะไปรบกวนวอร์ในปัจจุบัน.";
			}
			next;
			set .@i, select(" ~ ^0055FFReload ตั้งค่า...^000000: ~ ^777777ยกเลิก^000000");
			mes "[Reload Settings]";
			if (.@i == 2) mes "ยกเลิก.";
			else {
				set .Init,1;
				donpcevent "WOE_CONTROL::OnMinute00";
				mes "ได้รีสคริปแล้ว.";
			}
			next;
			break;
		case 4:
			mes "[Force Agit End]";
			if (!agitcheck() && !agitcheck2())  {
				mes "วอร์ได้จบลงแล้ว.";
				next;
				break;
			}
			mes "คำสั่งนี้จะรัน AgitEnd ทั้งหมด.";
			mes " ";
			mes "จบวอร์ที่กำลังตีกันอยู่?";
			next;
			set .@i, select(" ~ ^FF0000จบรอบ...^000000:"+((.Options&1)?" ~ ^FF0000จบรอบโดยไม่มีรางวัล...^000000":"")+": ~ ^777777ยกเลิก^000000");
			mes "[Force Agit End]";
			if (.@i == 3)
				mes "ยกเลิก.";
			else {
				set .ForceEnd, .@i;
				donpcevent "WOE_CONTROL::OnWOEEnd";
				mes "ยุติวอร์แล้ว.";
			}
			next;
			break;
		case 5:
			set .@d,1; break;
		}
		if (.@d) { set .@d,0; break; }
		} break;
	case 5:
		close;
	} }
}
